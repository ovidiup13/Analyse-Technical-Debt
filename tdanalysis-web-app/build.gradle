// Apply the application plugin to add support for building an application
apply plugin: 'application'

def webappDir = "$projectDir/src/main/webapp"

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java/']
        }
        resources {
            srcDirs = ['src/main/resources', "$webappDir/dist"]
        }
    }
    test {
        java {
            srcDirs = ['src/test/java/']
        }
    }
}

mainClassName = "com.td.App"
springBoot {
    mainClass = 'com.td.App'
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:$spring_boot_version"
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.0.1'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.0.2'
    
    compile project(':tdanalysis-db-module')
}

processResources {
	dependsOn "buildAngular"
}

task buildAngular(type:Exec) {
	// installAngular should be run prior to this task
	dependsOn "installAngular"
	workingDir "$webappDir"
	inputs.dir "$webappDir"
	// Add task to the standard build group
	group = BasePlugin.BUILD_GROUP
	// ng doesn't exist as a file in windows -> ng.cmd
	if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
		commandLine "yarn.cmd", "build"
	} else {
		commandLine "yarn", "build"
	}
}

task installAngular(type:Exec) {
	workingDir "$webappDir"
	inputs.dir "$webappDir"
	group = BasePlugin.BUILD_GROUP
	if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
		commandLine "yarn.cmd", "install"
	} else {
		commandLine "yarn", "install"
	}
}